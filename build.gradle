buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.20'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.2'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: "org.jetbrains.dokka"

group 'xyz.cuteclouds'
version '1.0.2'

repositories {
    jcenter()
    mavenLocal()
    //mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.20'
    compile 'xyz.cuteclouds:CloudyUtils:1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}



task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

task clearSpecific(type: Delete) {
    delete 'build/javadoc'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task docsZip(type: Zip, dependsOn: dokka) {
    classifier = 'docs'
    from "$buildDir/dokka"
}

jar {
    baseName = project.name
}

shadowJar {
    classifier = 'withDependencies'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

artifacts {
    archives shadowJar, javadocJar, sourcesJar, docsZip
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            //artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        //userOrg = 'cuteclouds'
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/adriantodt/HungerGames.git'
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn 'publishBintrayReleasePublicationToMavenLocal'
}
