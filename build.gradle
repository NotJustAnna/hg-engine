plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.jetbrains.dokka' version '0.9.17'
}

group = 'pw.aru.hg'
version = '1.0'

repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/arudiscord/hg' }
    mavenLocal()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31'
    testCompile 'pw.aru.hg:hg-tuples:1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task docsZip(type: Zip, dependsOn: dokka) {
    archiveClassifier = 'docs'
    from "$buildDir/dokka"
}

jar {
    baseName = project.name
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

artifacts {
    archives javadocJar, sourcesJar, docsZip
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'hg'
        name = project.name
        userOrg = 'arudiscord'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/arudiscord/hg-engine.git'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn publishToMavenLocal
}
